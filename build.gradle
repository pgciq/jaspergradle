apply plugin: 'java'

repositories {
    mavenCentral()
    jcenter()
    maven{url "http://jasperreports.sourceforge.net/maven2/"}
    maven{url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/"}
}

configurations {
    jasperreports {
        transitive = true
    }
}

gradle.projectsEvaluated {
    processResources.dependsOn(compileJasperReports)
}
dependencies {
    jasperreports 'net.sf.jasperreports:jasperreports:5.6.0',
            'org.codehaus.groovy:groovy-all:2.3.6'
}



task compileJasperReports {
    def jasperSourceDir = file('src/main/jasperreports')
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.jasperreports.asPath)
            sourceSets.main.output.classesDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: sourceSets.main.output.classesDir) {
                classpath(path: sourceSets.main.output.classesDir)
                include(name: '**/*.jrxml')
            }
        }
    }
}
